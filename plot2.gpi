#!/usr/bin/gnuplot

# terminal and png output
set terminal svg size 680,440 fname "Monospace" fsize "10" rounded dashed
set output "plot2.svg"

# remove border on top and right and set color to gray
set style line 11 lc rgb "#808080" lt 1
set border 3 back ls 11
set tics nomirror
# define grid
set style line 12 lc rgb "#808080" lt 0 lw 1
set grid back ls 12

# color definitions
set style line 1 lc rgb "#cc0000" pt 1 ps 1 lt 1 lw 2 # red
set style line 2 lc rgb "#ff7700" pt 2 ps 1 lt 1 lw 2 # orange
set style line 3 lc rgb "#009999" pt 3 ps 1 lt 1 lw 2 # light blue
set style line 4 lc rgb "#00cc00" pt 4 ps 1 lt 1 lw 2 # green
set style line 5 lc rgb "#2222bb" pt 5 ps 1 lt 1 lw 2 # dark blue
set style line 6 lc rgb "#cc0099" pt 6 ps 1 lt 1 lw 2 # purple

# labeling
set title "Experiment 2: Success vs Population Size" font "Monospace Bold,10"
set xlabel "Population Size"
set ylabel "Successes out of 30"

set key bottom right

# missing data points
set datafile missing "?"

plot "data2.dat" using 1:2 title "Uniform CO" w lp ls 1, \
     ""          using 1:3 title "Linear CO" w lp ls 2, \
     ""          using 1:4 title "Deceptive TF" w lp ls 3, \
     ""          using 1:5 title "Non-Deceptive TF" w lp ls 4, \
#
